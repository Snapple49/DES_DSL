grammar des.missionrobot.RobotDSL with org.eclipse.xtext.common.Terminals

generate robotDSL "http://www.missionrobot.des/RobotDSL"

Mission:
	'Mission' name = ID
	'UsedDevices:' deviceList += Device+
	'Behaviors:' behaviorList += Behavior+
	'Goal:' goalCondition += Trigger+
;

Behavior:
	'Behavior:' name = ID
	'priority:' prio = INT
	'devices:' deviceList += [Device]+
	'actions:' actionList += Action+
	'triggers:' triggerList += Trigger+
;

Action: 
	Movement ('Duration' duration = INT)? | Sound | IO
;

Movement: MoveForward |	MoveBackward | Turn | Stop;

MoveForward: 
	'Forward' fspeed = INT
;

MoveBackward: 
	'Backward' bspeed = INT
;

Turn: 
	'Turn' angle = INT
;

Stop:
	'Stop' duration = INT
;

Sound:
	'PlaySound:' soundName = STRING
;

IO:
	'PrintMessage' message = STRING
;

Trigger:
	'Condition:' condition = STRING
	('Sensor:' sensorTrigger = [Device])?
;

Device:
	'Device:' name = ID
	'type:' type = DeviceType
	'port:' port = STRING 
	('sensorType:' sensorType = SensorType)?
	('description:' desc = STRING)?
;

enum DeviceType:
	MOTOR = 'Motor' |
	SENSOR = 'Sensor'
;

enum SensorType:
	ULTRASONIC = 'Ultrasonic' |
	TOUCH = 'Touch' |
	COLOR = 'Color'	
;

