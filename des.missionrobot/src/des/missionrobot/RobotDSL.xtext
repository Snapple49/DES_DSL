grammar des.missionrobot.RobotDSL with org.eclipse.xtext.common.Terminals

generate robotDSL "http://www.missionrobot.des/RobotDSL"


Missions: 
	'Missionset' name = ID
	'Missions:' missionList += Mission
;

Mission:
	'Mission' name = ID
	'Flags:' flagsList += Flag*
	'Tasks:' taskList += Task+
	'Goal:' ('Condition:'goalEvents += Trigger+)? ('timeout' timeout = Time)? ('Actions:' finishActions += Action+)?
;

Task:
	'Task:' name = ID
	'priority:' prio = INT
	('Triggers:' triggerList += Trigger+)?
	'Actions:' actionList += Action+
;


Action: 
	'Move' dir = Direction ('for' duration = INT 'sec')? ('at' speed = Speed 'speed' )? | 
	'Turn' dir = Direction degr = INT 'deg' |
	'Arm' op = ArmOp |
	'Play' 'sound' sound = Sound |
	'Update' flag = [Flag] 'to' bool = Bool
;

Trigger: 
	(boolType = Bool)? 
	((neg = Negation)? flag = [Flag] |
	sensor = Sensor
	(('is' color = Color) | ('distance' dist = Distance )))
;

Distance:
	rangeBool = Bool 'than' distance = INT 'cm'
;

Flag:
	name = ID ('is' bool = Bool)?
;

Time:
	'Time:' sec = INT 'sec'
;

enum Direction:
	FORWARD = 'forward' |
	BACKWARD = 'backward' |
	LEFT = 	'left' |
	RIGHT = 'right'
;

enum Speed:
	HIGH = 'high' |
	MED = 'medium' |
	LOW = 'low'
;
enum ArmOp:
	UP = 'raise' |
	DOWN = 'lower'
;

enum Sound:
	buzz = 'buzz' |
	fanfare = 'fanfare'
;


enum Sensor:
	COLOR = 'Color' |
	LEFTLIGHT = 'LeftLight' |
	RIGHTLIGHT = 'RightLight' |
	BACKUS = 'RearHeight' |
	FRONTUS = 'FrontDistance' 	
;

enum Color:
	RED = 'red' |
	WHITE = 'white' |
	BLACK = 'black' |
	GREEN = 'green' |
	BLUE = 'blue' |
	BRIGHT = 'bright' |
	DARK = 'dark'
;

enum Bool:
	L = 'less' |
	G = 'greater' |
	AND = 'and' |
	OR = 'or' |
	TRUE = 'true' |
	FALSE = 'false'
;
enum Negation:
	NOT = 'not'
;
